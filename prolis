#!perl

=head1 NAME

procvt - List the prologues found in a file

=head1 SYNOPSIS

  prolis *.f
  cat test.f | prolis

=head1 DESCRIPTION

Takes the specified files, or standard input, extracts prologues and
writes them all to standard output (translating if necessary).

=head1 OPTIONS

=over 4

=item B<-version>

Report the version number.

=item B<-help>

Display the help information.

=item B<-man>

Display the full manual page.

=back

=cut

use strict;
use warnings;
use Pod::Usage;
use Getopt::Long;

use Starlink::Prologue;
use Starlink::Prologue::Parser;

# Options
my ($help, $man, $version);
my $status = GetOptions("help" => \$help,
                        "man" => \$man,
                        "version" => \$version,
                       );

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

if ($version) {
  my $id = '$Id$ ';
  print "prolis - List Starlink prologues found in source code\n";
  print " CVS revision: $id\n";
  exit;
}

# do we have standard input or file arguments?

if (@ARGV) {
  # files on command line
  for my $file (@ARGV) {
    open(my $fh, "<$file") || die "Error opening file $file for read: $!\n";
    _extract( $file, <$fh> );
  }
} else {
  # try reading from pipe
  my @lines = <>;
  if (@lines) {
    _extract( undef, @lines );
  }
}

exit;

sub _extract {
  my $file = shift;
  my @lines = @_;
  my $parser = new Starlink::Prologue::Parser;
  my @prologues = $parser->extract_prologues( @lines );

  # list them
  print STDERR "Extracted ".scalar(@prologues) . 
    " prologues from ". (defined $file ? "file $file" : "stdin")."\n";
  for my $prl (@prologues) {
    print $prl->stringify;
  }
  return;
}

=head1 SEE ALSO

C<procvt>

=head1 AUTHOR

Tim Jenness E<lt>t.jenness@jach.hawaii.eduE<gt>

Copyright 2006 Particle Physics and Astronomy Research Council.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful,but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 59 Temple
Place,Suite 330, Boston, MA  02111-1307, USA

=cut

1;
